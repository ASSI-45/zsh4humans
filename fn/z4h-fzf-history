# fzf-history-widget with duplicate removal, preview and syntax highlighting (requires `bat`).
#
# Disable preview:
#
#   zstyle :z4h:fzf-history fzf-flags --no-preview

eval "$_z4h_opt"

[[ -w $TTY && -r $TTY ]] || return

local preview='printf "%s" {} | command cut -f2- -d'$'\1'
(( $+commands[bat] )) && preview+=' | command bat -l bash --color always -pp --wrap=character --terminal-width=$((FZF_PREVIEW_COLUMNS-1))'

{
  -z4h-show-dots

  local esc line col
  IFS='[;' read -s -d R esc\?$'\e[6n' line col <$TTY || return
  print >$TTY || return

  typeset -ga _z4h_naturals
  if (( $#_z4h_naturals < $#history )); then
    _z4h_naturals+=({$(($#_z4h_naturals+1))..$#history})
  fi

  autoload +X -Uz -- -z4h-cursor-show

  {
    local choice
    choice=$(
      unsetopt pipe_fail
      {
        (( $#history )) && printf '%2$s\001%1$s\000' "${(@)${history[@]}:^_z4h_naturals}"
      } | {
        {
          -z4h-cursor-show
          2>$TTY -z4h-fzf --read0 --no-multi --tiebreak=index --cycle        \
              --delimiter='\001' --with-nth=2 --exact --no-mouse             \
              --height=80% --preview-window=down:40%:wrap --preview=$preview \
              --tabstop 1 --query=$LBUFFER --color=hl:201,hl+:201
        } always {
          -z4h-cursor-hide
        }
      })
    [[ -n $choice ]] || return
    zle .vi-fetch-history -n $(($#history - ${${choice#*$'\n'}%%$'\1'*} + 1))
    if [[ $KEYMAP == vicmd && -n $BUFFER ]]; then
      CURSOR=$(($#BUFFER - 1))
    else
      CURSOR=$#BUFFER
    fi
    if (( _z4h_use[zsh-autosuggestions] && _z4h_use[zsh-syntax-highlighting] )); then
      typeset -g _z4h_autosuggest_buffer=$BUFFER
      unset _z4h_autosuggestion POSTDISPLAY
      -z4h-redraw-buffer
    fi
    return 0
  } always {
    echoti cuu 1
    (( col > 1 )) && echoti cuf $((col-1))
  }
} always {
  zle -R
  -z4h-cursor-show
}
