local old=$Z4H.old.$$
local new=$Z4H.new.$$

{
  zf_rm -rf -- $old $new   || return
  zf_mkdir -p -- $new      || return
  print -n >$new/.updating || return

  Z4H=$new Z4H_UPDATING=$Z4H </dev/null >/dev/null $_z4h_exe -ic '
    (( $? )) && "exit" "1"
    "builtin" "emulate" "zsh" "-o" "no_aliases"
    [[ $Z4H == '${(q)new}' ]] || exit 0
    print -n >$Z4H/tmp/update-successful' || return

  if [[ ! -e $new/tmp/update-successful ]]; then
    local home=~
    local zdotdir=${${${(q)ZDOTDIR}/#${(q)home}/'~'}//\%/%%}
    local z4h=${${${(q)Z4H}/#${(q)home}/'~'}//\%/%%}
    print -Pru2 -- '%F{3}z4h%f: %B$Z4H%b %F{1}does not propagate%f through %U.zshrc%u'
    print -Pru2 -- ''
    print -Pru2 -- 'Change %U'$zdotdir'/.zshrc%u to keep %BZ4H%b intact if already set.'
    print -Pru2 -- ''
    print -Pru2 -- 'For example:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}:%f %F{3}"${Z4H:=${XDG_CACHE_HOME:-$HOME/.cache}/zsh4humans}"%f'
    print -Pru2 -- ''
    print -Pru2 -- 'Note: The leading colon (%F{2}:%f) is necessary.'
    return 1
  fi

  zf_rm -- $new/tmp/update-successful                    || return
  zf_rm -- $new/.updating                                || return
  if ! zf_mv -f -- $Z4H $old 2>/dev/null; then
    zf_rm -rf -- $Z4H/cache/gitstatus                    || return
    zf_mv -f -- $Z4H $old                                || return
  fi
  zf_mv -f -- $new $Z4H                                  || return
  zf_rm -rf -- $old                                      || return
} always {
  if (( $? )); then
    -z4h-error-command update
  fi
  zf_rm -rf -- $old $new || return
}

print -Pru2 -- "%F{3}z4h%f: %Bupdate successful%b"
print -Pru2 -- "%F{3}z4h%f: restarting %F{2}zsh%f"
exec -- $_z4h_exe -i || return
