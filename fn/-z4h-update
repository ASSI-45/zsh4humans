local old=$Z4H.old.$$
local new=$Z4H.new.$$

{
  zf_rm -rf -- $old $new || return 1
  zf_mkdir -p -- $new    || return 1

  local home=~
  local zdotdir=${${${(q)ZDOTDIR}/#${(q)home}/'~'}//\%/%%}
  local z4h=${${${(q)Z4H}/#${(q)home}/'~'}//\%/%%}

  Z4H=$new -z4h-clone romkatv/zsh4humans romkatv/zsh4humans ${Z4H_URL:t} || return
  $new/romkatv/zsh4humans/sc/setup -o $Z4H -n $new                       || return 1
  print -n >$new/cache/z4h-updating                                      || return 1
  Z4H=$new </dev/null >/dev/null $_z4h_exe -ic '
    (( $? )) && "exit" "1"
    "builtin" "emulate" "zsh" "-o" "no_aliases"
    [[ $Z4H == '${(q)new}' ]] || exit 0
    print -n >$Z4H/cache/z4h-update-succeeded'                           || return 1
  if [[ ! -e $new/cache/z4h-update-succeeded ]]; then
    print -Pru2 -- '%F{3}z4h%f: %B$Z4H%b %F{1}does not propagate%f through %U.zshrc%u'
    print -Pru2 -- ''
    print -Pru2 -- 'Change %U'$zdotdir'/.zshrc%u to keep %BZ4H%b intact if already set.'
    print -Pru2 -- ''
    print -Pru2 -- 'For example:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}:%f %F{3}"${Z4H:=${XDG_CACHE_HOME:-$HOME/.cache}/zsh4humans}"%f'
    print -Pru2 -- ''
    print -Pru2 -- 'Note: The leading colon (%F{2}:%f) is necessary.'
    return 2
  fi
  zf_rm -- $new/cache/z4h-update-succeeded || return 1
  zf_mv -f -- $Z4H $old                    || return 1
  zf_mv -f -- $new $Z4H                    || return 1
  zf_rm -rf -- $old                        || true
} always {
  if (( $? )); then
    print -Pru2 -- '%F{3}z4h%f: update %F{1}failed%f'
    print -Pru2 -- ''
    print -Pru2 -- 'See error messages above to identify the culprit.'
    print -Pru2 -- ''
    print -Pru2 -- 'Edit Zsh configuration:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}${(q)${VISUAL:-${EDITOR:-vi}}%f %U'$zdotdir'/.zshrc%u'
    print -Pru2 -- ''
    print -Pru2 -- 'Retry update:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}z4h%f %Bupdate%b'
    print -Pru2 -- ''
    print -Pru2 -- 'If errors persist and you are desperate:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{2}z4h%f %Breset%b'
    print -Pru2 -- ''
    print -Pru2 -- 'If nothing helps and you are about to give up:'
    print -Pru2 -- ''
    print -Pru2 -- '  %F{5}# nuke the entire site from orbit'
    print -Pru2 -- '  %F{2}%Usudo%u rm%f -rf -- %U'$z4h'%u'
    if (( $+commands[curl] )); then
      print -Pru2 -- ''
      print -Pru2 -- 'Give up and start over:'
      print -Pru2 -- ''
      print -Pru2 -- '  %F{2}sh%f -c %F{3}"%f$(%F{2}curl%f -fsSL %Uhttps://raw.githubusercontent.com/romkatv/zsh4humans/v2/install%u)%F{3}"%f'
    elif (( $+commands[wget] )); then
      print -Pru2 -- ''
      print -Pru2 -- 'Give up and start over:'
      print -Pru2 -- ''
      print -Pru2 -- '  %F{2}sh%f -c %F{3}"%f$(%F{2}wget%f -O- %Uhttps://raw.githubusercontent.com/romkatv/zsh4humans/v2/install%u)%F{3}"%f'
    fi
  fi
  zf_rm -rf -- $old $new || return
}

print -Pru2 -- "%F{3}z4h%f: %Bupdate successful%b"
print -Pru2 -- "%F{3}z4h%f: restarting %F{2}zsh%f"
exec -- $_z4h_exe -i || return
