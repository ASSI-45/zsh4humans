[[ -d $Z4H/$1 ]] && return

local dst=$Z4H/$1
local repo=$2
local branch=$3

print -Pru2 -- "%F{3}z4h%f: fetching %B${1//\%/%%}%b"
local old=$dst.old.$$
local new=$dst.new.$$
local url=https://github.com/$repo/archive/$branch.tar.gz
zf_mkdir -p -- $new || return
{
  (
    local err
    builtin cd -q -- $new || exit
    if (( $+commands[curl] )); then
      err="$(curl -fsSLo snapshot.tar.gz -- $url 2>&1)"
    elif (( $+commands[wget] )); then
      err="$(wget -O snapshot.tar.gz -- $url 2>&1)"
    else
      print -Pru2 -- "%F{3}z4h%f: please install %F{1}curl%f or %F{1}wget%f"
      exit 1
    fi
    if (( $? )); then
      print -ru2 -- $err
      print -Pru2 -- "%F{3}z4h%f: failed to download %F{1}${url//\%/%%}%f"
      exit 1
    fi
    tar -xzf snapshot.tar.gz || exit
  ) || return
  local dirs=($new/${repo:t}-*(N/))
  if (( $#dirs != 1 )); then
    print -Pru2 -- "%F{3}z4h%f: invalid content: %F{1}${url//\%/%%}%f"
    return 1
  fi
  [[ ! -e $dst ]] || zf_mv -- $dst $old || return
  zf_mv -- $dirs[1] $dst
} always {
  zf_rm -rf -- $old $new
}
