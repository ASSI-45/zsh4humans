# When a command is running, display it in the terminal title.
function -z4h-set-term-title-preexec() {
  eval "$_z4h_opt"
  if (( P9K_SSH )); then
    -z4h-set-term-title ${(%):-"%n@%m: "}$1
  else
    -z4h-set-term-title $1
  fi
}

# When no command is running, display the current directory in the terminal title.
function -z4h-set-term-title-precmd() {
  eval "$_z4h_opt"
  if (( P9K_SSH )); then
    -z4h-set-term-title ${(%):-"%n@%m: %~"}
  else
    -z4h-set-term-title ${(%):-"%~"}
  fi
}

autoload -Uz add-zsh-hook                           || return
add-zsh-hook -- preexec -z4h-set-term-title-preexec || return
add-zsh-hook -- precmd -z4h-set-term-title-precmd   || return

local pkg
typeset -gA _z4h_use
for pkg in fzf fzf-tab powerlevel10k zsh-completions zsh-autosuggestions zsh-syntax-highlighting; do
  zstyle -t :z4h:$pkg channel none || _z4h_use[$pkg]=1
done
typeset -grA _z4h_use

local p10k_cfg=
# If the current locale isn't UTF-8, change it to an UTF-8 one.
# Try in order: C.UTF-8, en_US.UTF-8, the first UTF-8 locale in lexicographical order.
() {
  [[ $langinfo[CODESET] == (utf|UTF)(-|)8 ]] && return 0
  (( $+commands[locale] )) || return
  local loc=(${(@M)$(locale -a):#*.(utf|UTF)(-|)8})
  (( $#loc )) || return
  export LC_ALL=${loc[(r)(#i)C.UTF(-|)8]:-${loc[(r)(#i)en_US.UTF(-|)8]:-$loc[1]}}
} && [[ $TERM != (dumb|linux) ]] || p10k_cfg+=-ascii
(( terminfo[colors] >= 256 )) || p10k_cfg+=-8color

if (( _z4h_use[powerlevel10k] )); then
  : ${POWERLEVEL9K_CONFIG_FILE:=$ZDOTDIR/.p10k${p10k_cfg}.zsh}
fi

# Enable command_not_found_handler if possible.
if (( $+functions[command_not_found_handler] )); then
  # already installed
elif [[ -e /etc/zsh_command_not_found ]]; then
  source /etc/zsh_command_not_found
elif [[ -e /usr/share/doc/pkgfile/command-not-found.zsh ]]; then
  source /usr/share/doc/pkgfile/command-not-found.zsh
elif [[ -x /usr/libexec/pk-command-not-found && -S /var/run/dbus/system_bus_socket ]]; then
  command_not_found_handler() { /usr/libexec/pk-command-not-found "$@" }
elif [[ -x /data/data/com.termux/files/usr/libexec/termux/command-not-found ]]; then
  command_not_found_handler() { /data/data/com.termux/files/usr/libexec/termux/command-not-found "$@" }
elif [[ -x /run/current-system/sw/bin/command-not-found ]]; then
  command_not_found_handler() { /run/current-system/sw/bin/command-not-found "$@" }
elif (( $+commands[brew] )); then
  () {
    eval "$_z4h_opt"
    [[ -n $TTY && ( -n $CONTINUOUS_INTEGRATION || -z $MC_SID ) ]] || return
    local repo
    repo="$(command brew --repository 2>/dev/null)" || return
    [[ -n $repo/Library/Taps/*/*/cmd/brew-command-not-found-init(|.rb)(#q.N) ]] || return
    function command_not_found_handler() {
      eval "$_z4h_opt"
      local msg
      if msg="$(command brew which-formula --explain $1 2>/dev/null)" && [[ -n $msg ]]; then
        print -ru2 -- $msg
      else
        print -ru2 -- "zsh: command not found: $1"
      fi
      return 127
    }
  }
fi

function z4h-up-local-history() {
  -z4h-with-local-history 1 up-line-or-beginning-search "$@"
}
function z4h-down-local-history() {
  -z4h-with-local-history 1 down-line-or-beginning-search "$@"
}
function z4h-up-global-history() {
  -z4h-with-local-history 0 up-line-or-beginning-search "$@"
}
function z4h-down-global-history() {
  -z4h-with-local-history 0 down-line-or-beginning-search "$@"
}

function z4h-forward-word()       { -z4h-with-forward-word  .forward-word       }
function z4h-kill-word()          { -z4h-with-forward-word  .kill-word          }
function z4h-backward-word()      { -z4h-with-backward-word .backward-word      }
function z4h-backward-kill-word() { -z4h-with-backward-word .backward-kill-word }

function z4h-beginning-of-buffer() { CURSOR=0 }
function z4h-end-of-buffer() { CURSOR=$#BUFFER }
function z4h-expand() { zle _expand_alias || zle .expand-word || true }

function -z4h-fzf() {
  eval "$_z4h_opt"
  print >$TTY
  {
    local choice again
    choice="$($Z4H/fzf/bin/fzf "$@")" || return
    if [[ $choice != $'\n'* ]]; then
      again=1
      print -r -- $'\n'${choice#*$'\n'}
    else
      print -r -- $choice
    fi
  } always {
    print -n "$again" >$_z4h_fzf_called
  }
}

function z4h-expand-or-complete() {
  local esc line col
  while true; do
    IFS='[;' read -s -d R esc\?$'\e[6n' line col <"$TTY" || return
    local lbuf="$LBUFFER"
    local post="$POSTDISPLAY"
    POSTDISPLAY='..'
    region_highlight+=(
      "${#BUFFER} $((${#BUFFER} + ${#POSTDISPLAY})) $ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE")
    zle -R
    {
      local _z4h_fzf_called="$Z4H"/tmp/fzf-called."${sysparams[pid]}"
      zf_rm -f -- "$_z4h_fzf_called" || return
      zle fzf-tab-complete
    } always {
      local -i err=$?
      local -a stat=()
      if zstat +size -A stat -- "$_z4h_fzf_called" 2>/dev/null; then
        zf_rm -f -- "$_z4h_fzf_called" || err=$?
        echoti cuu 1
        (( col > 1 )) && echoti cuf $((col-1))
      fi
      if (( err || !stat[1] )) || [[ "$LBUFFER" != */ || "$LBUFFER" == "$lbuf" ]]; then
        POSTDISPLAY="$post"
        region_highlight[-1]=()
        break
      fi
      zle .split-undo
      zle -R
      POSTDISPLAY="$post"
      region_highlight[-1]=()
    }
  done
}

function z4h-cd-back() { -z4h-cd-rotate +1 }
function z4h-cd-forward() { -z4h-cd-rotate -0 }
function z4h-cd-up() { cd .. && -z4h-redraw-prompt }

function z4h-do-nothing() {}

autoload -Uz up-line-or-beginning-search down-line-or-beginning-search || return
autoload -Uz run-help ${^fpath}/run-help-*(N:t)                        || return
(( $+aliases[run-help] )) && unalias run-help  # make run-help more useful

zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
zle -N z4h-expand
zle -N z4h-forward-word
zle -N z4h-kill-word
zle -N z4h-backward-word
zle -N z4h-backward-kill-word
zle -N z4h-beginning-of-buffer
zle -N z4h-end-of-buffer
zle -N z4h-stash-buffer
zle -N z4h-expand-or-complete
zle -N z4h-up-local-history
zle -N z4h-down-local-history
zle -N z4h-up-global-history
zle -N z4h-down-global-history
zle -N z4h-cd-back
zle -N z4h-cd-forward
zle -N z4h-cd-up
zle -N z4h-fzf-history
zle -N z4h-autosuggest-accept
zle -N z4h-do-nothing

PROMPT_EOL_MARK='%K{red} %k'   # mark the missing \n at the end of a comand output with a red block
READNULLCMD=less               # use `less` instead of the default `more`
WORDCHARS=''                   # only alphanums make up words in word-based zle widgets
ZLE_REMOVE_SUFFIX_CHARS=''     # don't eat space when typing '|' after a tab completion
zle_highlight=('paste:none')   # disable highlighting of text pasted into the command line

if (( ! _z4h_custom_histfile )); then
  HISTFILE=${ZDOTDIR:-~}/.zsh_history  # save command history in this file
fi
HISTSIZE=1000000000                    # infinite command history
SAVEHIST=1000000000                    # infinite command history

if (( _z4h_use[fzf] )); then
  if (( ! $+FZF_ALT_C_COMMAND )); then
    local prune=('-name '{.git,.hg,.svn} '-path "*/.*/*"')
    if [[ ${commands[find]:A} != */busybox* ]]; then
      prune+=('-fstype '{sysfs,devfs,devtmpfs,proc})
    fi
    prune="\( ${(j: -o :)prune} \) -prune"
    FZF_ALT_C_COMMAND="command find -L . -mindepth 1 $prune -o -type d -print 2>/dev/null | command cut -b3-"
  fi
  : ${FZF_COMPLETION_TRIGGER=}                   # file completion without trigger
  fzf_default_completion=z4h-expand-or-complete  # file completion falls back to regular
  z4h source $Z4H/fzf/shell/completion.zsh       # load fzf-completion
  z4h source $Z4H/fzf/shell/key-bindings.zsh     # load fzf-cd-widget
  unfunction -m '_fzf_complete_*'
fi

if (( _z4h_use[fzf-tab] )); then
  FZF_TAB_COMMAND=-z4h-fzf
  zstyle ':fzf-tab:*' prefix       ''         # remove 'Â·'
  zstyle ':fzf-tab:*' query-string prefix     # pre-populate fzf query with the longest common prefix
  bindkey '^I' expand-or-complete             # fzf-tab reads it during initialization
  z4h source $Z4H/fzf-tab/fzf-tab.zsh         # load fzf-tab-complete
  eval "function fzf-tab-complete() {
    ${functions[fzf-tab-complete]//zle (.reset-prompt|redisplay|-R)/}
  }"
fi

# Delete all existing keymaps and reset to the default state.
bindkey -d
bindkey -e

local keymap
for keymap in emacs viins vicmd; do
  # If NumLock is off, translate keys to make them appear the same as with NumLock on.
  bindkey -M $keymap -s '^[OM'     '^M'      # enter
  bindkey -M $keymap -s '^[Ok'     '+'
  bindkey -M $keymap -s '^[Om'     '-'
  bindkey -M $keymap -s '^[Oj'     '*'
  bindkey -M $keymap -s '^[Oo'     '/'
  bindkey -M $keymap -s '^[OX'     '='

  # If someone switches our terminal to application mode (smkx), translate keys to make
  # them appear the same as in raw mode (rmkx).
  bindkey -M $keymap -s '^[OA'     '^[[A'    # up
  bindkey -M $keymap -s '^[OB'     '^[[B'    # down
  bindkey -M $keymap -s '^[OD'     '^[[D'    # left
  bindkey -M $keymap -s '^[OC'     '^[[C'    # right
  bindkey -M $keymap -s '^[OH'     '^[[H'    # home
  bindkey -M $keymap -s '^[OF'     '^[[F'    # end

  # TTY sends different key codes. Translate them to xterm equivalents.
  # Missing: {ctrl,alt,shift}+{up,down,left,right,home,end}, {ctrl,alt}+delete.
  bindkey -M $keymap -s '^[[1~'    '^[[H'    # home
  bindkey -M $keymap -s '^[[4~'    '^[[F'    # end

  # Urxvt sends different key codes. Translate them to xterm equivalents.
  bindkey -M $keymap -s '^[[7~'    '^[[H'    # home
  bindkey -M $keymap -s '^[[8~'    '^[[F'    # end
  bindkey -M $keymap -s '^[Oa'     '^[[1;5A' # ctrl+up
  bindkey -M $keymap -s '^[Ob'     '^[[1;5B' # ctrl+down
  bindkey -M $keymap -s '^[Od'     '^[[1;5D' # ctrl+left
  bindkey -M $keymap -s '^[Oc'     '^[[1;5C' # ctrl+right
  bindkey -M $keymap -s '^[[7\^'   '^[[1;5H' # ctrl+home
  bindkey -M $keymap -s '^[[8\^'   '^[[1;5F' # ctrl+end
  bindkey -M $keymap -s '^[[3\^'   '^[[3;5~' # ctrl+delete
  bindkey -M $keymap -s '^[^[[A'   '^[[1;3A' # alt+up
  bindkey -M $keymap -s '^[^[[B'   '^[[1;3B' # alt+down
  bindkey -M $keymap -s '^[^[[D'   '^[[1;3D' # alt+left
  bindkey -M $keymap -s '^[^[[C'   '^[[1;3C' # alt+right
  bindkey -M $keymap -s '^[^[[7~'  '^[[1;3H' # alt+home
  bindkey -M $keymap -s '^[^[[8~'  '^[[1;3F' # alt+end
  bindkey -M $keymap -s '^[^[[3~'  '^[[3;3~' # alt+delete
  bindkey -M $keymap -s '^[[a'     '^[[1;2A' # shift+up
  bindkey -M $keymap -s '^[[b'     '^[[1;2B' # shift+down
  bindkey -M $keymap -s '^[[d'     '^[[1;2D' # shift+left
  bindkey -M $keymap -s '^[[c'     '^[[1;2C' # shift+right
  bindkey -M $keymap -s '^[[7$'    '^[[1;2H' # shift+home
  bindkey -M $keymap -s '^[[8$'    '^[[1;2F' # shift+end

  # Tmux sends different key codes. Translate them to xterm equivalents.
  bindkey -M $keymap -s '^[[1~'    '^[[H'    # home
  bindkey -M $keymap -s '^[[4~'    '^[[F'    # end
  bindkey -M $keymap -s '^[^[[A'   '^[[1;3A' # alt+up
  bindkey -M $keymap -s '^[^[[B'   '^[[1;3B' # alt+down
  bindkey -M $keymap -s '^[^[[D'   '^[[1;3D' # alt+left
  bindkey -M $keymap -s '^[^[[C'   '^[[1;3C' # alt+right
  bindkey -M $keymap -s '^[^[[1~'  '^[[1;3H' # alt+home
  bindkey -M $keymap -s '^[^[[4~'  '^[[1;3F' # alt+end
  bindkey -M $keymap -s '^[^[[3~'  '^[[3;3~' # alt+delete

  # iTerm2 sends different key codes. Translate them to xterm equivalents.
  # Missing (depending on settings): ctrl+{up,down,left,right}, {ctrl,alt}+{delete,backspace}.
  bindkey -M $keymap -s '^[^[[A'   '^[[1;3A' # alt+up
  bindkey -M $keymap -s '^[^[[B'   '^[[1;3B' # alt+down
  bindkey -M $keymap -s '^[^[[D'   '^[[1;3D' # alt+left
  bindkey -M $keymap -s '^[^[[C'   '^[[1;3C' # alt+right
  bindkey -M $keymap -s '^[[1;9A'  '^[[1;3A' # alt+up
  bindkey -M $keymap -s '^[[1;9B'  '^[[1;3B' # alt+down
  bindkey -M $keymap -s '^[[1;9D'  '^[[1;3D' # alt+left
  bindkey -M $keymap -s '^[[1;9C'  '^[[1;3C' # alt+right
  bindkey -M $keymap -s '^[[1;9H'  '^[[1;3H' # alt+home
  bindkey -M $keymap -s '^[[1;9F'  '^[[1;3F' # alt+end
done

# Move cursor one char backward.
bindkey   '^[[D'    backward-char                  # left
# Move cursor one char forward.
bindkey   '^[[C'    forward-char                   # right
# Move cursor one line up or fetch the previous command from LOCAL history.
bindkey   '^P'      z4h-up-local-history           # ctrl+p
bindkey   '^[[A'    z4h-up-local-history           # up
# Move cursor one line down or fetch the next command from LOCAL history.
bindkey   '^N'      z4h-down-local-history         # ctrl-n
bindkey   '^[[B'    z4h-down-local-history         # down
# Move cursor one line up or fetch the previous command from GLOBAL history.
bindkey   '^[[1;5A' z4h-up-global-history          # ctrl+up
# Move cursor one line down or fetch the next command from GLOBAL history.
bindkey   '^[[1;5B' z4h-down-global-history        # ctrl+down
# Move cursor to the beginning of line.
bindkey   '^[[H'    beginning-of-line              # home
# Move cursor to the end of line.
bindkey   '^[[F'    end-of-line                    # end
# Move cursor to the beginning of buffer.
bindkey   '^[[1;5H' z4h-beginning-of-buffer        # ctrl+home
bindkey   '^[[1;3H' z4h-beginning-of-buffer        # alt+home
# Move cursor to the end of buffer.
bindkey   '^[[1;5F' z4h-end-of-buffer              # ctrl+end
bindkey   '^[[1;3F' z4h-end-of-buffer              # alt+end
# Delete the character under the cursor.
bindkey   '^D'      delete-char                    # delete
bindkey   '^[[3~'   delete-char                    # delete
# Delete next word.
bindkey   '^[d'     z4h-kill-word                  # alt+d
bindkey   '^[D'     z4h-kill-word                  # alt+D
bindkey   '^[[3;5~' z4h-kill-word                  # ctrl+del
bindkey   '^[[3;3~' z4h-kill-word                  # alt+del
# Delete previous word.
bindkey   '^W'      z4h-backward-kill-word         # ctrl+w
bindkey   '^[^H'    z4h-backward-kill-word         # ctrl+alt+bs
bindkey   '^[^?'    z4h-backward-kill-word         # alt+bs

# Delete line before cursor.
bindkey   '^[k'     backward-kill-line             # alt+k
bindkey   '^[K'     backward-kill-line             # alt+K
# Delete all lines.
bindkey   '^[j'     kill-buffer                    # alt+j
bindkey   '^[J'     kill-buffer                    # alt+J
# Push buffer to ephemeral history (won't be saved to HISTFILE) and delete all lines.
bindkey   '^[o'     z4h-stash-buffer               # alt+o
bindkey   '^[O'     z4h-stash-buffer               # alt+O
# Accept autosuggestion.
bindkey   '^[m'     z4h-autosuggest-accept         # alt+m
bindkey   '^[M'     z4h-autosuggest-accept         # alt+M
# Redo.
bindkey   '^[/'     redo                           # alt+/
# Expand alias/glob/parameter.
bindkey   '^ '      z4h-expand                     # ctrl+space
if (( _z4h_use[fzf-tab] )); then
  # Generic command completion.
  bindkey '^I'      z4h-expand-or-complete         # tab
fi
if (( _z4h_use[fzf] )); then
  # Deep file completion.
  bindkey '^[i'     fzf-completion                 # alt+i
  bindkey '^[I'     fzf-completion                 # alt+I
  # Command history.
  bindkey '^R'      z4h-fzf-history                # ctrl+r
fi
# Show help for the command at cursor.
bindkey   '^[h'     run-help                       # alt+h
bindkey   '^[H'     run-help                       # alt+H
# Do nothing (better than printing '~').
bindkey   '^[[5~'   z4h-do-nothing                 # pageup
bindkey   '^[[6~'   z4h-do-nothing                 # pagedown

# Move cursor one word backward.
bindkey   '^[b'     z4h-backward-word              # alt+b
bindkey   '^[B'     z4h-backward-word              # alt+B

# Move cursor one word forward.
bindkey   '^[f'     z4h-forward-word               # alt+f
bindkey   '^[F'     z4h-forward-word               # alt+F

if zstyle -t :z4h: cd-key ctrl; then
  # Move cursor one word backward.
  bindkey '^[[1;3D' z4h-backward-word              # alt+left
  # Move cursor one word forward.
  bindkey '^[[1;3C' z4h-forward-word               # alt+right
  # cd into the previous directory.
  bindkey '^[[1;5D' z4h-cd-back                    # ctrl+left
  # cd into the next directory.
  bindkey '^[[1;5C' z4h-cd-forward                 # ctrl+right
  # cd into the parent directory.
  bindkey '^[[1;5A' z4h-cd-up                      # ctrl+up
  if (( _z4h_use[fzf] )); then
    # cd into a subdirectory (interactive).
    bindkey '^[[1;5B' fzf-cd-widget                # ctrl+down
  fi
else
  # Move cursor one word backward.
  bindkey '^[[1;5D' z4h-backward-word              # ctrl+left
  # Move cursor one word forward.
  bindkey '^[[1;5C' z4h-forward-word               # ctrl+right
  # cd into the previous directory.
  bindkey '^[[1;3D' z4h-cd-back                    # alt+left
  # cd into the next directory.
  bindkey '^[[1;3C' z4h-cd-forward                 # alt+right
  # cd into the parent directory.
  bindkey '^[[1;3A' z4h-cd-up                      # alt+up
  if (( _z4h_use[fzf] )); then
    # cd into a subdirectory (interactive).
    bindkey '^[[1;3B' fzf-cd-widget                # alt+down
  fi
fi

# Use lesspipe if available. It allows you to use less on binary files (zip archives, etc.).
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

# This affects every invocation of `less`.
#
#   -i   case-insensitive search unless search string contains uppercase letters
#   -R   color
#   -F   exit if there is less than one page of content
#   -X   keep content on screen after exit
#   -M   show more info at the bottom prompt line
#   -x4  tabs are 4 instead of 8
export LESS='-iRFXMx4'

export PAGER=less

# LS_COLORS is used by GNU ls and Zsh completions. LSCOLORS is used by BSD ls.
export LS_COLORS='rs=0:no=00:mi=00:mh=00:ln=01;36:or=01;31:di=01;34:ow=04;01;34:st=34:tw=04;34:'
LS_COLORS+='pi=01;33:so=01;33:do=01;33:bd=01;33:cd=01;33:su=01;35:sg=01;35:ca=01;35:ex=01;32:'
export LSCOLORS='ExGxDxDxCxDxDxFxFxexEx'

# Configure completions.
zstyle ':completion:*'                  matcher-list    'm:{a-zA-Z}={A-Za-z}' 'l:|=* r:|=*'
zstyle ':completion:*'                  verbose         yes
zstyle ':completion:::::'               insert-tab      pending
zstyle ':completion:*'                  completer       _complete
zstyle ':completion:*:*:-subscript-:*'  tag-order       indexes parameters
zstyle ':completion:*'                  squeeze-slashes true
zstyle ':completion:*'                  single-ignored  show
zstyle ':completion:*:(rm|kill|diff):*' ignore-line     other
zstyle ':completion:*:rm:*'             file-patterns   '*:all-files'
zstyle ':completion:*'                  use-cache       true
zstyle ':completion:*'                  cache-path      $Z4H/cache/zcompcache-$ZSH_VERSION
zstyle ':completion:*'                  list-colors     ${(s.:.)LS_COLORS}

if (( _z4h_use[powerlevel10k] )); then
  # Initialize prompt. Type `p10k configure` or edit $POWERLEVEL9K_CONFIG_FILE to customize it.
  () {
    local XDG_CACHE_HOME=$Z4H/cache/powerlevel10k
    z4h source $Z4H/powerlevel10k/powerlevel10k.zsh-theme
  }
  z4h source $POWERLEVEL9K_CONFIG_FILE
  -z4h-set-term-title-precmd || return
fi

if (( _z4h_use[zsh-autosuggestions] )); then
  if (( terminfo[colors] >= 256 )); then
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=244'        # the default is hard to see
  else
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=black,bold' # the default is outside of 8-color range
  fi

  ZSH_AUTOSUGGEST_MANUAL_REBIND=1

  # Tell zsh-autosuggestions how to handle different widgets.
  typeset -g ZSH_AUTOSUGGEST_EXECUTE_WIDGETS=()
  typeset -g ZSH_AUTOSUGGEST_CLEAR_WIDGETS=(
    zle-line-init
    zle-line-finish
    z4h-up-local-history
    z4h-down-local-history
    z4h-up-global-history
    z4h-down-global-history
    z4h-fzf-history
  )
  typeset -g ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS=(
    z4h-forward-word
    forward-word
    emacs-forward-word
    vi-forward-word
    vi-forward-word-end
    vi-forward-blank-word
    vi-forward-blank-word-end
    vi-find-next-char
    vi-find-next-char-skip
  )
  typeset -g ZSH_AUTOSUGGEST_ACCEPT_WIDGETS=(
    end-of-line
    vi-end-of-line
    vi-add-eol
    z4h-end-of-buffer
  )

  if zstyle -T :z4h:autosuggestions forward-char accept; then
    ZSH_AUTOSUGGEST_ACCEPT_WIDGETS+=(forward-char vi-forward-char)
  else
    ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS+=(forward-char vi-forward-char)
  fi

  z4h source $Z4H/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

if (( _z4h_use[zsh-syntax-highlighting] )); then
  if (( terminfo[colors] >= 256 )); then
    typeset -gA ZSH_HIGHLIGHT_STYLES=(comment fg=96)  # the default is hard to see
  fi
  ZSH_HIGHLIGHT_MAXLENGTH=1024                # don't colorize long command lines (slow)
  ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)  # main syntax highlighting plus matching brackets
fi

if (( _z4h_use[zsh-syntax-highlighting] && _z4h_use[zsh-autosuggestions] )); then
  if (( $+widgets[zle-line-pre-redraw] )); then
    zle -A -- zle-line-pre-redraw -z4h-orig-zle-line-pre-redraw
  fi
  zle -N -- zle-line-pre-redraw -z4h-zle-line-pre-redraw

  precmd_functions=(${precmd_functions:#_zsh_autosuggest_start})

  eval '
    _zsh_autosuggest_highlight_reset() {
      [[ -z "${POSTDISPLAY-}" ]] || region_highlight[-1]=()
    }
    _zsh_autosuggest_highlight_apply() {
      if [[ -n "${POSTDISPLAY-}" ]]; then
        region_highlight+=(
          "${#BUFFER} $((${#BUFFER} + ${#POSTDISPLAY})) $ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE")
      fi
    }
    _zsh_autosuggest_clear() {
      _zsh_autosuggest_invoke_original_widget "$@"
      local -i retval=$?
      if (( ! retval )); then
        typeset -g _z4h_autosuggest_buffer="$BUFFER"
        unset _z4h_autosuggestion
      fi
      -z4h-autosuggest-fetch
      return retval
    }
    _zsh_autosuggest_modify() {
      _zsh_autosuggest_invoke_original_widget "$@"
      local -i retval=$?
      -z4h-autosuggest-fetch
      return retval
    }
    _zsh_autosuggest_accept() {
      if (( CURSOR < ${#BUFFER} -1 )) || [[ $CURSOR -lt ${#BUFFER} && "$KEYMAP" != "vicmd" ]]; then
        _zsh_autosuggest_invoke_original_widget "$@"
        return
      fi
      -z4h-autosuggest-fetch
      () { '$functions[_zsh_autosuggest_accept]' } "$@"
      local -i retval=$?
      -z4h-autosuggest-store
      return retval
    }
    _zsh_autosuggest_partial_accept() {
      if (( CURSOR < ${#BUFFER} -1 )) || [[ $CURSOR -lt ${#BUFFER} && "$KEYMAP" != "vicmd" ]]; then
        _zsh_autosuggest_invoke_original_widget $@
        return
      fi
      -z4h-autosuggest-fetch
      () { '$functions[_zsh_autosuggest_partial_accept]' } "$@"
      local -i retval=$?
      -z4h-autosuggest-store
      return retval
    }
    _zsh_autosuggest_execute() {
      -z4h-autosuggest-fetch
      () { '$functions[_zsh_autosuggest_execute]' } "$@"
      local -i retval=$?
      -z4h-autosuggest-store
      return retval
    }'
fi

function -z4h-post-init() {
  eval "$_z4h_opt"

  add-zsh-hook -d -- precmd -z4h-post-init

  # Initialize completions.
  if zstyle -t ':completion::complete:' use-cache; then
    local cache
    zstyle -s ':completion::complete:' cache-path cache
    : ${cache:=${ZDOTDIR:-~}/.zcompcache}
    if [[ ! -e $cache ]]; then
      zf_mkdir -m 0700 -p -- $cache
    fi
  fi

  local dump
  zstyle -s ':z4h:compinit' dump-path dump
  : ${dump:=$Z4H/cache/zcompdump-$ZSH_VERSION}
  unfunction compinit compdef
  autoload -Uz compinit
  compinit -u -d $dump
  [[ ! -r $dump ]] || -z4h-compile $dump

  # Replay compdef calls.
  local args
  for args in $_z4h_compdef; do
    compdef "${(@0)args}"
  done
  unset _z4h_compdef

  # Make it possible to use completion specifications and functions written for bash.
  autoload -Uz bashcompinit
  bashcompinit

  if (( _z4h_use[zsh-syntax-highlighting] && _z4h_use[zsh-autosuggestions] )); then
    () {
      local -hA widgets=()
      z4h source $Z4H/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
      eval '_zsh_highlight() {
        if [[ "${#BUFFER}" -gt "${ZSH_HIGHLIGHT_MAXLENGTH:-${#BUFFER}}" ]]; then
          region_highlight=()
          return
        fi
        local -ih PENDING=0 KEYS_QUEUED_COUNT=0
        '$functions[_zsh_highlight]'
      }'
    }
    local suggest_special=(
      zle-line-init
      zle-line-finish
      zle-isearch-update
      $ZSH_AUTOSUGGEST_EXECUTE_WIDGETS
      $ZSH_AUTOSUGGEST_CLEAR_WIDGETS
      $ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS
      $ZSH_AUTOSUGGEST_ACCEPT_WIDGETS)
    typeset -g ZSH_AUTOSUGGEST_IGNORE_WIDGETS=(${${(k)widgets}:|suggest_special})
    unset ZSH_AUTOSUGGEST_USE_ASYNC
    _zsh_autosuggest_start
  elif (( _z4h_use[zsh-syntax-highlighting] )); then
    z4h source $Z4H/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
  fi
}

precmd_functions=(-z4h-post-init $precmd_functions)

[[ -e $Z4H/z4h.zsh.zwc ]] || -z4h-compile-all

# Aliases.
if (( $+commands[dircolors] )); then  # proxy for GNU coreutils vs BSD
  # Don't define aliases for commands that point to busybox.
  [[ ${${:-diff}:c:A:t} == busybox* ]] || alias diff='diff --color=auto'
  [[ ${${:-ls}:c:A:t}   == busybox* ]] || alias ls='ls --color=auto'
else
  [[ ${${:-ls}:c:A:t}   == busybox* ]] || alias ls='ls -G'
fi
[[ ${${:-grep}:c:A:t}   == busybox* ]] || alias grep='grep --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn}'
