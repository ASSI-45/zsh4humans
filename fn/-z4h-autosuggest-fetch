if (( ${+_z4h_redraw_fd} )); then
  zle -F "$_z4h_redraw_fd"
  exec {_z4h_redraw_fd}>&-
  unset _z4h_redraw_fd
fi

if [[ -z "$BUFFER" || "$CONTEXT" != "start" ]]; then
  unset POSTDISPLAY _z4h_autosuggest_buffer _z4h_autosuggestion
else
  () {
    if [[ "$BUFFER" == "${_z4h_autosuggest_buffer-}"* ]]; then
      if (( $#BUFFER == $#_z4h_autosuggest_buffer )); then
        POSTDISPLAY="${_z4h_autosuggestion-}"
        return
      elif (( ${+_z4h_autosuggestion} )) &&
           [[ "$_z4h_autosuggestion" == "${BUFFER:$#_z4h_autosuggest_buffer}"* ]]; then
        POSTDISPLAY="${_z4h_autosuggestion:$(($#BUFFER - $#_z4h_autosuggest_buffer))}"
        typeset -g _z4h_autosuggest_buffer="$BUFFER"
        typeset -g _z4h_autosuggestion="$POSTDISPLAY"
        return
      fi
    fi
    local suggestion
    if [[ "$#BUFFER" -le "${ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE:-$#BUFFER}" ]]; then
      _zsh_autosuggest_fetch_suggestion "$BUFFER"
    fi
    POSTDISPLAY="${suggestion:$#BUFFER}"
    typeset -g _z4h_autosuggest_buffer="$BUFFER"
    typeset -g _z4h_autosuggestion="$POSTDISPLAY"
  }
  [[ -n "$POSTDISPLAY" ]] || unset POSTDISPLAY
fi
