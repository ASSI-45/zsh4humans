local -i i
local -a pos
for ((i = 1; i <= $#; ++i)); do
  case $*[i] in
    --) (( ++i <= $# )) && pos+=({$i..$#}); break;;
    -O) command ssh "$@"; return;;
    -*) [[ bcDEeFIiJLlmOopQRSWw == *${${*[i]}[-1]}* ]] && ((++i));;
    *)  pos+=($i);;
  esac
done

if (( $#pos != 1 )); then
  command ssh "$@"
  return
fi

local user_host=$*[pos[1]]
local host=${user_host##*@}
if [[ -z $host ]] || zstyle -t :z4h:ssh:$host passthrough; then
  command ssh "$@"
  return
fi

local -a set_files add_files
if ! zstyle -a :z4h:ssh:$host set-files set_files; then
  set_files=(.zshenv .zprofile .zshrc .zlogin .zlogout)
fi
if ! zstyle -a :z4h:ssh:$host add-files add_files; then
  add_files=(.p10k{,-ascii}{,-8color}.zsh)
fi

local overlap=(${set_files:*add_files})
if (( $#overlap )); then
  overlap='%1F'${(j:%f,%1F :)${(@q)overlap//\%/%%}}'%f'
  print -Pru2 -- '%F{3}z4h%f: listed both in %Bset-files%b and in %Badd-files%b: '$overlap
  return 1
fi

if (( ! $set_files[(Ie).zshrc] && ! $add_files[(Ie).zshrc] )); then
  print -Pru2 -- '%F{3}z4h%f: %U.zshrc%u is listed neither in %Bset-files%b nor in %Badd-files%b'
  return 1
fi

local -aU all_files=($ZDOTDIR/$^set_files(N-.) $ZDOTDIR/$^cfg_add(N-.))
all_files=(${all_files#$ZDOTDIR/})
if (( ! $all_files[(Ie).zshrc] )); then
  print -Pru2 -- '%F{3}z4h%f: file not found: %F{1}'${ZDOTDIR//\%/%%}'/.zshrc%f'
  return 1
fi

local script connection=${(%):-%n}:$host
script="$(<$Z4H/zsh4humans/sc/ssh-bootstrap)" || return
script=${script/'^CONNECTION^'/${(qqq)connection}}
script=${script/'^SET_FILES^'/${(j: :)${(@qqq)set_files}}}
script=${script/'^DUMP_POS^'/${(r:8:: :)${#script}}}

local file=.z4h-ssh.${(%):-%n}.$sysparams[pid].$EPOCHSECONDS.$RANDOM
{
  print -r -- $script >$Z4H/tmp/$file                          || return
  command tar -C $ZDOTDIR -czh -- $all_files >>$Z4H/tmp/$file  || return
  local args=("$@")
  args[pos[1],pos[1]-1]=('-T')
  command ssh "${args[@]}" "/bin/cat >~/$file" <$Z4H/tmp/$file || return
} always {
  zf_rm -f -- $Z4H/tmp/$file
}

args[pos[1]]='-t'
command ssh "${args[@]}" "/bin/sh ~/$file" || return
