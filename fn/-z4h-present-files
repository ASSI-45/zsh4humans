eval "$_z4h_opt"
setopt octalzeroes

local -i list_colors=$1
local -i list_types=$1

[[ $_z4h_mode_colors[ln] != target ]]
local -i ln_target=$?

local slash=('/')
local suf=('' '' '' '/' '' '' '' '' '' '@')

local -i10 m
local buf file
local -a files mode link
local MATCH MBEGIN MEND

while true; do
  while [[ $buf != *$'\n'* ]]; do
    sysread -s 4096 'buf[$#buf+1]' && continue
    (( $? == 5 ))                  || return
    return
  done
  files=("${(@f)buf}")
  buf=$files[-1]
  files[-1]=()
  if (( ! list_colors && ! list_types )) || ! zstat -L -A mode +mode -- $files; then
    printf '%1$s\0%1$q\n' $files
    continue
  fi
  if (( ! list_colors )); then
    printf '%2$s\0%2$q%1$s\n' ${"${(@)mode/(#m)*/$suf[$(((MATCH & 0170000) >> 12))]}":^files}
    continue
  fi
  for file m in ${files:^mode}; do
    if (( (m & 0170000) == 0120000 )); then
      if ! zstat -L -A link +link -- $file; then
        printf '%1$s\0\e[%2$sm%1$q\e[0m\n' \
          "$file" "${_z4h_mode_colors[or]:-$_z4h_name_colors[(k)${file:t}]}"
      elif ! zstat -A mode +mode -- $file; then
        printf '%1$s\0\e[%2$sm%1$q\e[0m -> \e[%4$sm%3$q\e[0m\n'              \
          "$file" "${_z4h_mode_colors[or]:-$_z4h_name_colors[(k)${file:t}]}" \
          "$link" "${_z4h_mode_colors[or]:-$_z4h_name_colors[(k)${link:t}]}" 
      else
        m=mode[1]
        if (( ln_target )); then
          printf '%1$s\0\e[%3$sm%1$q\e[0m -> \e[%3$sm%2$q\e[0m\n'               \
            "$file" "$link$slash[$(( list_types && (m & 0170000) == 040000 ))]" \
            "${_z4h_mode_codes[$((m & 64002 | ((m & 73) != 0)))]:-$_z4h_name_colors[(k)${link:t}]}"
        else
          printf '%1$s\0\e[%2$sm%1$q\e[0m -> \e[%4$sm%3$q\e[0m\n'              \
            "$file" "${_z4h_mode_colors[ln]:-$_z4h_name_colors[(k)${file:t}]}" \
            "$link$slash[$(( list_types && (m & 0170000) == 040000 ))]"        \
            "${_z4h_mode_codes[$((m & 64002 | ((m & 73) != 0)))]:-$_z4h_name_colors[(k)${link:t}]}"
        fi
      fi
    else
      printf '%1$s\0\e[%3$sm%1$q%2$s\e[0m\n'                           \
        "$file" "$slash[$(( list_types && (m & 0170000) == 040000 ))]" \
        "${_z4h_mode_codes[$((m & 64002 | ((m & 73) != 0)))]:-$_z4h_name_colors[(k)${file:t}]}"
    fi
  done
done
